AC_INIT([IUMFS], [0.1.2], [admin2@whiteboard.ne.jp])
AC_PROG_CC
AC_PROG_INSTALL

AC_ARG_ENABLE([64bit],
   [  --disable-64bit         disable build of 64-bit driver],
[use_64bit="$enableval"],[use_64bit=yes])

if test "$use_64bit" = "yes"
then
AC_CHECK_PROG(ISAINFO, isainfo, yes, no, /usr/bin)
else
ISAINFO=no
fi
if test "$ISAINFO" = "yes" -a "$use_64bit" = "yes";
then 
    KARCH=`/usr/bin/isainfo -k`
else
    KARCH=`uname -p`
fi

IS_GNU_LD=`ld --version 2>&1 | grep  GNU`
if test "$GCC" = yes; then
	CFLAGS="$CFLAGS -Wall"
	PTHREAD_CFLAGS="-pthreads"
else
	PTHREAD_CFLAGS="-mt"
fi

MAJOR_VERSION=`uname -r | cut -f 1 -d .`
MINOR_VERSION=`uname -r | cut -f 2 -d .`
if test "$MAJOR_VERSION" -ge 5  -a "$MINOR_VERSION" -ge 11 ; then
	CFLAGS="$CFLAGS -DSOL11 -DSOL10"
else
	CFLAGS="$CFLAGS -DSOL10"
fi

case $KARCH in
     'sparc' | 'i386')
	KCFLAGS="$CFLAGS"
	DRV_DIR="/usr/kernel/drv"
	FS_DIR="/kernel/fs"
	;;
      'sparcv9')
	CFLAGS="$CFLAGS -m64"
	KCFLAGS="$CFLAGS"
	DRV_DIR="/usr/kernel/drv/sparcv9"
	if test -n "$IS_GNU_LD";
	then
		LD_OPT="-melf64_sparc"
	fi
	FS_DIR="/kernel/fs/sparcv9"
	;;
      'amd64')
	CFLAGS="$CFLAGS -m64"
	if test "$GCC" = yes; then
		KCFLAGS="$CFLAGS -mcmodel=kernel -mno-red-zone"
	else
		KCFLAGS="$CFLAGS -xmodel=kernel"
	fi
	DRV_DIR="/usr/kernel/drv/amd64"
	if test -n "$IS_GNU_LD";
	then
		LD_OPT="-melf_x86_64"
	fi
	FS_DIR="/kernel/fs/amd64"
	;;
esac

#KCFLAGS="$KCFLAGS -D_KERNEL -I. -D_LARGEFILE64_SOURCE=1"
KCFLAGS="$KCFLAGS -D_KERNEL -I." 

AC_ARG_ENABLE(debug,
[  --enable-debug          Enable debuging],
   AC_DEFINE(DEBUG, 1)
)

AC_SUBST(CFLAGS)
AC_SUBST(KCFLAGS)
AC_SUBST(LD_OPT)
AC_SUBST(DRV_DIR)
AC_SUBST(FS_DIR)
AC_SUBST(CC)
AC_SUBST(PTHREAD_CFLAGS)

AC_CONFIG_FILES([Makefile kernel/Makefile iumfs-test/Makefile])
AC_OUTPUT
